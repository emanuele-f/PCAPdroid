apply plugin: 'com.android.application'

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"
    ndkVersion "24.0.8215888"

    defaultConfig {
        applicationId "com.emanuelef.remote_capture"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 49
        versionName "1.5.0-preview"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Use a different app ID for debug to allow its coexistence with release builds
            //applicationIdSuffix '.debug'
            versionNameSuffix '-play'

            if(project.hasProperty("doNotStrip")) {
                // NOTE: packagingOptions will also affect the "release" build, so the "doNotStrip"
                // guard is required
                packagingOptions {
                    // NOTE: unstripped nDPI library takes up about 4 MB!
                    //doNotStrip '**.so'
                    doNotStrip '**/libpcapd.so'
                    doNotStrip '**/libcapture.so'
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.18.1"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += '../submodules/MaxMind-DB-Reader-java/src/main/java'
    }

    testOptions {
        // needed by robolectric
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    def billing_version = "4.0.0"

// Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.8"
    testImplementation 'org.mockito:mockito-core:1.10.19'

// AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.navigation:navigation-fragment:2.4.2"
    implementation "androidx.navigation:navigation-ui:2.4.2"

// Google
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.brotli:dec:0.1.2'

// Third-party
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'com.github.KaKaVip:Android-Flag-Kit:v0.1'

    implementation "com.android.billingclient:billing:$billing_version"
}
