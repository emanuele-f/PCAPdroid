apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "31.0.0"
    ndkVersion "23.0.7599858"

    defaultConfig {
        applicationId "com.emanuelef.remote_capture"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 36
        versionName "1.4.2"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            if(project.hasProperty("doNotStrip")) {
                // NOTE: packagingOptions will also affect the "release" build, so the "doNotStrip"
                // guard is required
                packagingOptions {
                    // NOTE: unstripped nDPI library takes up about 4 MB!
                    //doNotStrip '**.so'
                    doNotStrip '**/libpcapd.so'
                    doNotStrip '**/libvpnproxy-jni.so'
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.18.1"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def billing_version = "4.0.0"

// AndroidX
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

// Google
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.7'

// Third-party
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    implementation 'com.google.android.gms:play-services-ads:20.4.0'
    implementation "com.android.billingclient:billing:$billing_version"
}