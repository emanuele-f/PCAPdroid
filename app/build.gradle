apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    ndkVersion "23.1.7779620"

    defaultConfig {
        applicationId "com.emanuelef.remote_capture"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 42
        versionName "1.4.5"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Use a different app ID for debug to allow its coexistence with release builds
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            if(project.hasProperty("doNotStrip")) {
                // NOTE: packagingOptions will also affect the "release" build, so the "doNotStrip"
                // guard is required
                packagingOptions {
                    // NOTE: unstripped nDPI library takes up about 4 MB!
                    //doNotStrip '**.so'
                    doNotStrip '**/libpcapd.so'
                    doNotStrip '**/libcapture.so'
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.18.1"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += '../submodules/MaxMind-DB-Reader-java/src/main/java'
    }

    testOptions {
        // needed by robolectric
        unitTests.includeAndroidResources = true
    }
}

dependencies {
// Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.7.3"
    testImplementation 'org.mockito:mockito-core:1.10.19'

// AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

// Google
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.9'

// Third-party
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.github.KaKaVip:Android-Flag-Kit:v0.1'
}